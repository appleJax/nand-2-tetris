// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Inc16(in=updatedRegister, out=add1);

    Mux8Way16(
        a=updatedRegister,
        b=add1,
        c=in,
        d=in,
        e=false,
        f=false,
        g=false,
        h=false,
        sel[0]=inc,
        sel[1]=load,
        sel[2]=reset,
        out=newRegister
    );

    Or(a=reset, b=load, out=resetOrLoad);
    Or(a=resetOrLoad, b=inc, out=shouldUpdate);
    Register(in=newRegister, load=shouldUpdate, out=out, out=updatedRegister);
}
